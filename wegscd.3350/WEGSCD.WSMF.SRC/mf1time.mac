 /* ---------------------------------------------------------------- */
 /* RESULTS OF TIME CALCULATION FROM AN SMF7X HEADER                 */
 /* ---------------------------------------------------------------- */
 DCL  1  MF1T                   UNALIGNED,
     5 MF1SDATE FIXED DEC(7),   /* MF/1 INTERVAL START 00YYDDDF */
     5 MF1STIME FIXED DEC(7),   /* MF/1 INTERVAL START 0HHMMSSF */
     5 MF1EDATE FIXED DEC(7),   /* MF/1 INTERVAL END   00YYDDDF */
     5 MF1ETIME FIXED DEC(7),   /* MF/1 INTERVAL END   0HHMMSSF */
     5 MF1INT   FIXED DEC(7),   /* MF/1 INTERVAL LENGTH MMSSTTTF */
     5 MF1INTF  FLOAT;          /* MF/1 INTERVAL LENGTH SSS.TTT */

 /* DCL CALCMF1TIME ENTRY; */

 CALCMF1TIME: PROC(P);
   DCL P POINTER;
   DCL 1 SMF7X UNALIGNED BASED(P),
 %INCLUDE SMF7X;

   DCL 1 PX LIKE SMF7X;

   DCL (HH, MM, SS, CARRY) FIXED BIN(31);

   PX = SMF7X;
   PUT FILE(REPORT) SKIP DATA(PX);

   MF1T.MF1SDATE = SMF7X.SMF7XDAT;
   MF1T.MF1STIME = SMF7X.SMF7XIST;
   MF1T.MF1INT = SMF7X.SMF7XINT;
   MF1T.MF1INTF = TRUNC(SMF7X.SMF7XINT / 1.0E5) * 60 +
     MOD(SMF7X.SMF7XINT, 1.0E5) / 1.0E3;

   MF1T.MF1EDATE = SMF7X.SMF7XDAT;
   MF1T.MF1ETIME = SMF7X.SMF7XIST;

   SS = MOD (MF1T.MF1ETIME, 100);
   MM = MOD (MF1T.MF1ETIME / 100, 100);
   HH = MOD (MF1T.MF1ETIME / 10000, 100);
   PUT FILE(REPORT) SKIP(2) DATA(MF1T.MF1INT);
   PUT FILE(REPORT) SKIP(2) DATA(MF1T.MF1INTF);
   PUT FILE(REPORT) SKIP(2) DATA(MF1T.MF1ETIME);
   PUT FILE(REPORT) SKIP(2) DATA(HH, MM, SS);

   SS = SS + MF1T.MF1INTF;
   CARRY = SS / 60;
   SS = MOD(SS, 60);

   MM = MM + CARRY;
   CARRY = MM / 60;
   MM = MOD(MM, 60);

   HH = HH + CARRY;
   CARRY = HH / 24;
   HH = MOD(HH, 24);
   MF1T.MF1ETIME = (HH * 10000) + (MM * 100) + SS;

   /* TODO: IF CARRY IS 1, THEN WE NEED TO BUMP MF1T.MF1EDATE */
 %DECLARE DEBUG FIXED;
 %DEBUG = 0;
 %IF (DEBUG) %THEN %DO;
   PUT FILE(REPORT) SKIP(2) DATA(CARRY);
   PUT FILE(REPORT) SKIP(2) DATA(HH, MM, SS);
 %END;
   PUT FILE(REPORT) SKIP(2) DATA(MF1T);
 END CALCMF1TIME;
